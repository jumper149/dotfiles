pkgname=xmobar-git
pkgver=0.29.5.r34.gd571f0e
pkgrel=1
pkgdesc='Minimalistic Text Based Status Bar'
url='https://hackage.haskell.org/package/xmobar'
license=('custom:BSD3')
arch=('x86_64')
depends=('libxft' 'libxinerama' 'libxrandr' 'libxpm' 'ghc-libs' 'haskell-x11'
         'haskell-x11-xft' 'haskell-utf8-string' 'haskell-network-uri'
         'haskell-hinotify' 'haskell-stm' 'haskell-parsec' 'haskell-parsec-numbers'
         'haskell-mtl' 'haskell-regex-base' 'haskell-regex-compat'
         'haskell-http' 'haskell-dbus' 'haskell-libmpd' 'haskell-iwlib'
         'wireless_tools' 'haskell-text' 'haskell-async' 'haskell-extensible-exceptions')
makedepends=('git' 'ghc')
conflicts=('xmobar')
provides=('xmobar')
source=(${pkgname}::git+https://github.com/jaor/xmobar.git)
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed -E 's|\.r0\.\w+$||'
}

prepare() {
  cd ${pkgname}
  sed -i -e 's/==.*0.3/== 0.4/' -e 's/< *4.12/<5/' -e 's/< *2.5/<3/' xmobar.cabal
}

build() {
  cd ${pkgname}
  runhaskell setup configure -O \
    --enable-shared \
    --prefix=/usr \
    --enable-executable-dynamic \
    --disable-library-vanilla \
    --flags="with_utf8 with_xft with_xpm with_iwlib with_mpd with_dbus with_mpris with_weather with_inotify"
  runhaskell setup build
}

package() {
  cd ${pkgname}
  runhaskell setup copy --destdir="${pkgdir}"
  install -Dm 644 license "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
